_cfgdir=/etc/nginx
_tmpdir=/var/lib/nginx

pkgname=nginx-rune
pkgver=1.4.7
pkgrel=1
pkgdesc="NGiNX HTTP server (RuneAudio)"
arch=('armv6h')
depends=('pcre' 'zlib' 'openssl' 'apr-util')
makedepends=('git')
url="http://nginx.org"
license=('custom')
conflicts=('nginx')
provides=('nginx')

backup=(${_cfgdir:1}/fastcgi.conf
        ${_cfgdir:1}/fastcgi_params
        ${_cfgdir:1}/koi-win
        ${_cfgdir:1}/koi-utf
        ${_cfgdir:1}/mime.types
        ${_cfgdir:1}/nginx.conf
        ${_cfgdir:1}/scgi_params
        ${_cfgdir:1}/uwsgi_params
        ${_cfgdir:1}/win-utf
        etc/logrotate.d/nginx)

source=(http://nginx.org/download/nginx-$pkgver.tar.gz
        service
        nginx.logrotate)

build() {
	cd "$srcdir"

	msg "Fetching nginx-push-stream-module"
	_modules=(${_modules[@]} --add-module=../nginx-push-stream-module)
	if [ -d nginx-push-stream-module ]; then
		pushd nginx-push-stream-module
		git pull
		popd
	else
		git clone https://github.com/wandenberg/nginx-push-stream-module.git
	fi

	cd "$srcdir/nginx-$pkgver"

	msg "Building nginx"

	CFLAGS="-I /usr/include/httpd -I /usr/include/apr-1" ./configure \
        --prefix=$_cfgdir \
        --conf-path=$_cfgdir/nginx.conf \
        --sbin-path=/usr/bin/nginx \
        --pid-path=/var/run/nginx.pid \
        --lock-path=/var/lock/nginx.lock \
        --user=http --group=http \
        --http-log-path=/var/log/runeaudio/runeui.log \
        --error-log-path=/var/log/runeaudio/runeui.log \
        --http-client-body-temp-path=$_tmpdir/client-body \
        --http-proxy-temp-path=$_tmpdir/proxy \
        --http-fastcgi-temp-path=$_tmpdir/fastcgi \
        --http-scgi-temp-path=$_tmpdir/scgi \
        --http-uwsgi-temp-path=$_tmpdir/uwsgi \
        --with-file-aio \
        --with-http_gunzip_module \
        --with-http_gzip_static_module \
        --with-http_realip_module \
        --with-http_spdy_module \
        --with-http_stub_status_module \
        --without-mail_pop3_module --without-mail_imap_module --without-mail_smtp_module \
        ${_modules[@]} \


	make
}

package() {
	cd "$srcdir"/nginx-$pkgver
	make DESTDIR="$pkgdir" install

	sed -e 's|\<user\s\+\w\+;|user html;|g' \
	    -e '2s|worker_processes  1|worker_processes auto|' \
	    -e '44s|html|/usr/share/nginx/html|' \
	    -e '54s|html|/usr/share/nginx/html|' \
	    -i "$pkgdir"$_cfgdir/nginx.conf
	rm "$pkgdir"/etc/nginx/*.default


	install -d "$pkgdir"/$_tmpdir
	install -d "$pkgdir"/var/log
	install -d "$pkgdir"/usr/share/nginx
	mv "$pkgdir"/etc/nginx/html/ "$pkgdir"/usr/share/nginx

	install -Dm644 "$srcdir"/nginx.logrotate "$pkgdir"/etc/logrotate.d/nginx
	install -Dm644 "$srcdir"/service "$pkgdir"/usr/lib/systemd/system/nginx.service
	install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/nginx/LICENSE
	rm -rf "$pkgdir"/var/run
}


sha256sums=('23b8ff4a76817090678f91b0efbfcef59a93492f6612dc8370c44c1f1ce1b626'
            '05fdc0c0483410944b988d7f4beabb00bec4a44a41bd13ebc9b78585da7d3f9b'
            '9523a1fdd5eb61bf62f3049f6ee088b198e36d5edcce2d9b08bbeb2930aa5a16')
